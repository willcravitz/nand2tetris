// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 *
 * Implemented in opposite order of conditional statements to make sure top statement overrides
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16(in=prev, out=inced); // increment output
    Mux16(a=prev, b=inced, sel=inc, out=a); // select between incremented or normal
    Mux16(a=a, b=in, sel=load, out=b); // select between input or passed from previous gate
    Mux16(a=b, b=false, sel=reset, out=c); // select between all zeroes or passed from previous gate
    Register(in=c, load=true, out=out, out=prev); // load into register for time delay
}
